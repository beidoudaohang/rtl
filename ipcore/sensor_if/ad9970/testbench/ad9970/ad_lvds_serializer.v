//-------------------------------------------------------------------------------------------------
//  -- 版权所有者   : 中国大恒（集团）有限公司北京图像视觉技术分公司, 2010 -2015.
//  -- 保密级别     ：绝密.
//  -- 部门         : 硬件部，FPGA工作组
//  -- 模块名       : ad_lvds_serializer
//  -- 设计者       : 邢海涛
//-------------------------------------------------------------------------------------------------
//
//  -- 版本记录 :
//
//  -- 作者         :| 修改日期				:|  修改说明
//-------------------------------------------------------------------------------------------------
//  -- 邢海涛       :| 2015/8/9 14:11:30	:|  初始版本
//-------------------------------------------------------------------------------------------------
//
//  -- 模块描述     :
//              1)  : ... ...
//
//              2)  : ... ...
//
//              3)  : ... ...
//
//-------------------------------------------------------------------------------------------------
//仿真单位/精度
`timescale 1ns/1ps
//-------------------------------------------------------------------------------------------------

module ad_lvds_serializer (
	input				clk				,	//串行时钟
	input				reset			,	//串行时钟复位
	input	[15:0]		iv_pix_data		,	//像素数据
	//lvds端口
	output				o_tckp			,	//差分时钟 p端
	output				o_tckn			,	//差分时钟 n端
	output				o_dout0p		,	//差分数据0 p端
	output				o_dout0n		,	//差分数据0 n端
	output				o_dout1p		,	//差分数据1 p端
	output				o_dout1n			//差分数据1 n端
	);

	//	ref signals
	reg		[2:0]		ser_cnt			= 3'b0;
	reg		[15:0]		pix_data_dly0	= 16'b0;
	reg		[7:0]		shifter_ch0		= 8'b0;
	reg		[7:0]		shifter_ch1		= 8'b0;
	reg					clk_ser_dly		= 1'b0;

	//	ref ARCHITECTURE

	//	-------------------------------------------------------------------------------------
	//	串行化计数器
	//	1.每个通道的串化率是8bit，因此计数器是3bit，从0到7累加
	//	-------------------------------------------------------------------------------------
	always @ (posedge clk) begin
		if(reset) begin
			ser_cnt	<= 3'b0;
		end
		else begin
			ser_cnt	<= ser_cnt + 1'b1;
		end
	end

	//	-------------------------------------------------------------------------------------
	//	从仿真上看，lvds serializer通道也会有1拍的延时
	//	-------------------------------------------------------------------------------------
	always @ (posedge clk) begin
		if(ser_cnt==3'h7) begin
			pix_data_dly0	<= iv_pix_data;
		end
	end

	//	-------------------------------------------------------------------------------------
	//	串行通道1
	//	1.当串行化计数器=7时，更新并行寄存器的数值
	//	2.当串行化计数器是其他数值时，将最高位移出
	//	-------------------------------------------------------------------------------------
	always @ (posedge clk) begin
		if(ser_cnt==3'h7) begin
			shifter_ch1	<= pix_data_dly0[15:8];
		end
		else begin
			shifter_ch1	<= {shifter_ch1[6:0],1'b0};
		end
	end

	//	-------------------------------------------------------------------------------------
	//	串行通道0
	//	1.当串行化计数器=7时，更新并行寄存器的数值
	//	2.当串行化计数器是其他数值时，将最高位移出
	//	-------------------------------------------------------------------------------------
	always @ (posedge clk) begin
		if(ser_cnt==3'h7) begin
			shifter_ch0	<= pix_data_dly0[7:0];
		end
		else begin
			shifter_ch0	<= {shifter_ch0[6:0],1'b0};
		end
	end

	//	-------------------------------------------------------------------------------------
	//	时钟2分频
	//	-------------------------------------------------------------------------------------
	always @ (posedge clk) begin
		clk_ser_dly	<= !clk_ser_dly;
	end

	//	-------------------------------------------------------------------------------------
	//	lvds输出
	//	-------------------------------------------------------------------------------------
	assign	o_tckp		= clk_ser_dly;
	assign	o_tckn		= !clk_ser_dly;
	assign	o_dout0p	= shifter_ch0[7];
	assign	o_dout0n	= !shifter_ch0[7];
	assign	o_dout1p	= shifter_ch1[7];
	assign	o_dout1n	= !shifter_ch1[7];


endmodule
